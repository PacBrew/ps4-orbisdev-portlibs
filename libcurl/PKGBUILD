# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-orbisdev-libcurl
pkgver=7.80.0
pkgrel=1
pkgdesc='An URL retrieval utility and library'
arch=('any')
url='https://curl.haxx.se/'
license=('curl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.xz")
sha256sums=('a132bd93188b938771135ac7c1f3ac1d3ce507c1fcbef8c471397639214ae2ab')
options=(!strip libtool staticlibs)
depends=('ps4-orbisdev-zlib')
makedepends=('ps4-orbisdev-pkg-config' 'ps4-orbisdev-vars')
groups=('ps4-orbisdev-portlibs')

build() {
  cd curl-$pkgver

  source /opt/pacbrew/ps4/orbisdev/ps4vars.sh

  autoreconf -fi

  ./configure --prefix="${ORBISDEV}/usr" --host=x86_64 \
    --disable-shared --enable-static \
    --disable-manual --disable-ntlm-wb \
    --without-ssl --without-polar-ssl --without-cyassl --without-wolfssl \
    --without-mbedtls --without-libpsl --without-libgsasl

  # TODO: fix this...
  sed -i 's|#define HAVE_FREEADDRINFO|//#define HAVE_FREEADDRINFO|g' lib/curl_config.h
  sed -i 's|#define HAVE_GETADDRINFO|//#define HAVE_GETADDRINFO|g' lib/curl_config.h
  sed -i 's|#define HAVE_IF_NAMETOINDEX|//#define HAVE_IF_NAMETOINDEX|g' lib/curl_config.h
  sed -i 's|#define HAVE_GETHOSTBYNAME_R|//#define HAVE_GETHOSTBYNAME_R|g' lib/curl_config.h
  sed -i 's|#define HAVE_GETPWUID_R|//#define HAVE_GETPWUID_R|g' lib/curl_config.h
  sed -i 's|#define HAVE_GMTIME_R|//#define HAVE_GMTIME_R|g' lib/curl_config.h
  sed -i 's|#define HAVE_FNMATCH|//#define HAVE_FNMATCH|g' lib/curl_config.h
  sed -i 's|#define HAVE_GETHOSTBYNAME|//#define HAVE_GETHOSTBYNAME|g' lib/curl_config.h
  sed -i 's|#define HAVE_GETPWUID|//#define HAVE_GETPWUID|g' lib/curl_config.h
  # TODO: fix this more...
  sed -i 's|h = gethostbyname((void \*)hostname);|h = NULL; free(buf);|g' lib/hostip4.c

  make -C lib

}

package() {
  cd curl-$pkgver

  source /opt/pacbrew/ps4/orbisdev/ps4vars.sh

  make DESTDIR="$pkgdir" -C lib install
  make DESTDIR="$pkgdir" -C include install
  make DESTDIR="$pkgdir" install-binSCRIPTS
}
