# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-orbisdev-freetype
pkgver=2.10.1
pkgrel=2
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.gz")
sha256sums=('3a60d391fd579440561bf0e7f31af2222bc610ad6ce4d9d7bd2165bca8669110')
options=(!strip libtool staticlibs)
depends=("ps4-orbisdev-bzip2" "ps4-orbisdev-libpng" "ps4-orbisdev-zlib")
groups=('ps4-orbisdev-portlibs')
makedepends=('ps4-orbisdev-pkg-config' 'ps4-orbisdev-vars')

build() {
  cd freetype-$pkgver

  source /opt/pacbrew/ps4/orbisdev/ps4vars.sh

  ./configure --prefix="${ORBISDEV}/usr" --host=x86_64 \
    --disable-shared --enable-static \
    --with-zlib \
    --with-bzip2 \
    --with-png \
    --enable-freetype-config

  make
}

package() {
  cd freetype-$pkgver

  source /opt/pacbrew/ps4/orbisdev/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # patch the config script to always output static libraries when not relying on pkg-config
  sed 's/\$show_static/yes/' -i "${pkgdir}/${ORBISDEV}/usr/bin/freetype-config"

  # remove useless stuff
  rm -r "${pkgdir}/${ORBISDEV}/usr/share"
}
