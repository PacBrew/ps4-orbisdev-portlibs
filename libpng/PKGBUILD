# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-orbisdev-libpng
pkgver=1.6.37
pkgrel=1
pkgdesc='PNG format graphic files library'
arch=('any')
url='http://www.libpng.org'
license=('custom')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")
sha256sums=('505e70834d35383537b6491e7ae8641f1a4bed1876dbfe361201fc80868d88ca')
options=(!strip libtool staticlibs)
depends=('ps4-orbisdev-zlib')
makedepends=('ps4-orbisdev-vars')
groups=('ps4-orbisdev-portlibs')

build() {
  cd libpng-$pkgver

  source /opt/pacbrew/ps4/orbisdev/ps4vars.sh

  sed -i 's/^bin_PROGRAMS = .*//' Makefile.in

  ./configure --prefix="${ORBISDEV}/usr" --host=x86_64 \
    --disable-shared --enable-static

  make
}

package() {
  cd libpng-$pkgver
  make LN_S=cp DESTDIR="$pkgdir" install
}
